!*******************************************************************************
!
!> @file  DG_cfl.f90
!> @brief contains module DG_cfl
!
!*******************************************************************************
! MODULE DESCRIPTION:
!> @brief computation of CFL number
!
MODULE DG_cfl

  USE GRID_api
  USE FLASH_parameters
  USE DG_equation, ONLY : velocity, pressure
  USE IO_equation, ONLY : p_equationparam

  PRIVATE
  PUBLIC  :: compute_cfl

  CONTAINS

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE compute_cfl]:
!> @brief computation of CFL number according to local signal speeds
!
!> @param[in]     p_ghand         grid handling data structure
!> @param[in]     r_Q             state vector with all dofs
!> @param[in]     r_S             vector of source terms with all dofs
!> @param[in]     r_hmin          grid parameter for local cell size
!> @param[in]     i_faceunknowns  DOFs per element
!> @param[in]     i_elmtnodes     element-node relation
!> @param[in]     i_elmtdofs      element-DOF relation
!> @param[in,out] p_tsinfo        runtime information structure
!> @param[out]    r_cfllocal      local CFL numbers
!
  SUBROUTINE compute_cfl(p_ghand, r_Q, r_S, r_hmin, i_faceunknowns, i_elmtnodes, &
                         i_elmtdofs, p_tsinfo, r_cfllocal)

    IMPLICIT NONE

    TYPE(grid_handle), INTENT(in)                         :: p_ghand
    REAL (KIND = GRID_SR), DIMENSION(:,:), INTENT(in)     :: r_Q, r_S
    REAL (KIND = GRID_SR), DIMENSION(:), INTENT(in)       :: r_hmin
    INTEGER (KIND = GRID_SI), INTENT(in)                  :: i_faceunknowns
    INTEGER (KIND = GRID_SI), DIMENSION(:,:), INTENT(in)  :: i_elmtnodes, i_elmtdofs
    TYPE (rt_info), INTENT(inout)                         :: p_tsinfo
    REAL (KIND = GRID_SR), DIMENSION(:), INTENT(out)      :: r_cfllocal

    INTEGER (KIND = GRID_SI)                              :: i_elmt, i_dof
    REAL (KIND = GRID_SR), DIMENSION(i_faceunknowns)      :: r_uabs, r_pressure

    p_tsinfo%r_velmax = 0.0_GRID_SR

    DO i_elmt = 1, p_ghand%i_enumfine
!--- compute velocity
      r_uabs = SQRT(velocity(r_Q(1,i_elmtdofs(:,i_elmt)), r_Q(2,i_elmtdofs(:,i_elmt)))**2 + &
                    velocity(r_Q(1,i_elmtdofs(:,i_elmt)), r_Q(3,i_elmtdofs(:,i_elmt)))**2)
      p_tsinfo%r_velmax = MAX(p_tsinfo%r_velmax, MAXVAL(r_uabs))

      DO i_dof = 1, i_faceunknowns
        r_pressure(i_dof) = pressure(r_Q(:,i_elmtdofs(i_dof,i_elmt)))
      END DO

!--- estimate cfl = max(U+\sqrt(\phi))*\Delta t / \Delta x based on values at DOF points
      r_cfllocal(i_elmt) = &
        MAXVAL(r_uabs + SQRT(p_equationparam%r_gamma*r_pressure/r_Q(1,i_elmtdofs(:,i_elmt)))) * &
        p_tsinfo%r_timestep / MINVAL(r_hmin(i_elmtnodes(:,i_elmt)))

    END DO

!--- update timestep info structure
    p_tsinfo%r_cflnumber = MAXVAL(r_cfllocal)
    p_tsinfo%r_sshmax    = MAXVAL(r_Q(1,:))
    p_tsinfo%r_sshmin    = MINVAL(r_Q(1,:))

  END SUBROUTINE compute_cfl

!*******************************************************************************
END MODULE DG_cfl
