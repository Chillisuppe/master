!*******************************************************************************
!
!> @file  DG_initial_conicalisland-bc.f90
!> @brief contains module DG_initial
!
!*******************************************************************************
! MODULE DESCRIPTION:
!> @brief initialize fields for the conical island test case
!>        Note that the background fluid depth is set with BATHY_PARAM_DEPTH
!>        in the Parameters file.
!
MODULE DG_initial

  USE GRID_api
  USE FLASH_parameters
  USE DG_equation, ONLY : i_nprogvars, i_nsrcterms, i_valQ, i_valS
  USE IO_equation, ONLY : p_equationparam

  PRIVATE
  PUBLIC  i_ntstintparam, i_ntstrealparam, i_ntstcharparam, i_ntstlogparam, &
          initial_setparam, p_testparam, fvm_initialvalues, fvm_initialvalues_iter, &
          source_update, dg_elmt_solution, boundaryhgt

  TYPE (test_param)                                   :: p_testparam
  INTEGER, PARAMETER                                  :: i_ntstintparam  = 0
  INTEGER, PARAMETER                                  :: i_ntstrealparam = 3
  INTEGER, PARAMETER                                  :: i_ntstcharparam = 0
  INTEGER, PARAMETER                                  :: i_ntstlogparam  = 0

  CONTAINS

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE initial_setparam]:
!> @brief initializes test case specific parameter list
!
  SUBROUTINE initial_setparam

    IMPLICIT NONE

    ! amplitude of solitary wave
    p_testparam%treal(1)%c_keyword = 'PARAM_A'
    p_testparam%treal(1)%i_size    = 1

    ! time shift of solitary wave
    p_testparam%treal(2)%c_keyword = 'PARAM_T'
    p_testparam%treal(2)%i_size    = 1

    ! spatial shift of solitary wave
    p_testparam%treal(3)%c_keyword = 'PARAM_X0'
    p_testparam%treal(3)%i_size    = 1

  END SUBROUTINE initial_setparam

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE fvm_initialvalues]:
!> @brief initializes a grid with values
!>
!> @param[in]       p_ghand       grid handling data structure
!> @param[in,out]   p_param       control structure for global parameters
!> @param[in]       r_time        current model time
!>
!> @note This is just a dummy in this case.
!
  SUBROUTINE fvm_initialvalues(p_ghand, p_param, r_time)

    IMPLICIT NONE

    TYPE (grid_handle), INTENT(in)                        :: p_ghand
    TYPE (control_struct), INTENT(inout)                  :: p_param
    REAL (KIND = GRID_SR), INTENT(in)                     :: r_time

    CALL fvm_initialvalues_iter(p_ghand, p_param, r_time)

  END SUBROUTINE fvm_initialvalues

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE fvm_initialvalues_iter]:
!> @brief initializes a grid with values
!>
!> @param[in]       p_ghand       grid handling data structure
!> @param[in,out]   p_param       control structure for global parameters
!> @param[in]       r_time        current model time
!>
!> @note This routine initializes the flow field.
!
  SUBROUTINE fvm_initialvalues_iter(p_ghand, p_param, r_time)

    IMPLICIT NONE

    TYPE (grid_handle), INTENT(in)                        :: p_ghand
    TYPE (control_struct), INTENT(inout)                  :: p_param
    REAL (KIND = GRID_SR), INTENT(in)                     :: r_time

!--- local declarations
    INTEGER (KIND = GRID_SI)                              :: i_faceunknowns, i_numelmt, &
      i_num, i_alct
    INTEGER (KIND = GRID_SI), DIMENSION(:,:), ALLOCATABLE :: i_elmtdofs
    REAL (KIND = GRID_SR), DIMENSION(:,:), ALLOCATABLE    :: r_coodof, r_Q, r_S

!--- initialize some constants
    i_faceunknowns = GRID_femtypes%p_type(FEM_DG)%sig%i_unknowns
    i_numelmt      = p_ghand%i_enumfine
    i_num          = i_numelmt*i_faceunknowns

!--- allocate workspace
    ALLOCATE(i_elmtdofs(i_faceunknowns, i_numelmt), &
             r_coodof(GRID_dimension, i_num), &
             r_Q(i_nprogvars,i_num), r_S(i_nsrcterms, i_num), STAT=i_alct)
    IF(i_alct /= 0) CALL grid_error(c_error='[fvm_initialvalues_iter]: could not allocate grid field')

!--- get grid information
    CALL grid_getinfo(p_ghand, i_femtype=FEM_DG, l_relative=.TRUE., &
                      l_finelevel=.TRUE., i_elementdofs=i_elmtdofs, r_dofcoordinates=r_coodof)

!--- initialize fields
    r_Q = 0.0_GRID_SR
    r_S = 0.0_GRID_SR
    CALL source_update(p_param, r_time, i_numelmt, i_elmtdofs, r_coodof, .TRUE., &
                       r_Q, r_S)
    CALL field_init(p_param, i_numelmt, i_elmtdofs, r_coodof, r_Q, r_S)

!--- update grid information
    CALL grid_putinfo(p_ghand, l_finelevel=.TRUE., i_arraypoint=i_valQ, r_dofvalues=r_Q)
    CALL grid_putinfo(p_ghand, l_finelevel=.TRUE., i_arraypoint=i_valS, r_dofvalues=r_S)

!--- deallocate workspace
    DEALLOCATE(i_elmtdofs, r_coodof, r_Q, r_S)

  END SUBROUTINE fvm_initialvalues_iter

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE source_update]:
!> @brief updates the source terms
!>
!> @param[in]       p_param         control structure for global parameters
!> @param[in]       r_time          model time at which source term is evaluated
!> @param[in]       i_numelmt       number of elements
!> @param[in]       i_elmtdofs      global element-dof relation matrix
!> @param[in]       r_coodof        coordinates of DOFs
!> @param[in]       l_gridchanged
!> @param[in,out]   r_Q             discrete solution vector
!> @param           r_S             discrete fields used in source terms
!
  SUBROUTINE source_update(p_param, r_time, i_numelmt, i_elmtdofs, r_coodof, &
                           l_gridchanged, r_Q, r_S)

    IMPLICIT NONE

    TYPE (control_struct),                    INTENT(in)    :: p_param
    REAL (KIND = GRID_SR),                    INTENT(in)    :: r_time
    INTEGER (KIND = GRID_SI),                 INTENT(in)    :: i_numelmt
    INTEGER (KIND = GRID_SI), DIMENSION(:,:), INTENT(in)    :: i_elmtdofs
    REAL (KIND = GRID_SR), DIMENSION(:,:),    INTENT(in)    :: r_coodof
    LOGICAL,                                  INTENT(in)    :: l_gridchanged
    REAL (KIND = GRID_SR), DIMENSION(:,:),    INTENT(inout) :: r_Q, r_S

!--- local declarations
    INTEGER (KIND = GRID_SI)                                :: i_faceunknowns, &
      i_elmt, i_dof, i_globaldof
    REAL (KIND = GRID_SR)                                   :: r_radius, r_aux
    REAL (KIND = GRID_SR), DIMENSION(GRID_dimension)        :: r_centr, r_tmp

    IF (l_gridchanged) THEN
!--- initialize some constants
      i_faceunknowns = GRID_femtypes%p_type(FEM_DG)%sig%i_unknowns
      r_centr = [12.96_GRID_SR, 13.8_GRID_SR]

      DO i_elmt=1, i_numelmt
        DO i_dof=1, i_faceunknowns
          i_globaldof = i_elmtdofs(i_dof,i_elmt)
          r_aux = r_Q(1,i_globaldof) + r_S(1,i_globaldof)
          r_tmp(:)           = r_coodof(:,i_globaldof) - r_centr(:)
          r_radius           = SQRT(DOT_PRODUCT(r_tmp, r_tmp))
          IF (r_radius < 1.1_GRID_SR) THEN
            r_S(1,i_globaldof) = 0.625_GRID_SR * GRID_GRAV
          ELSE IF (r_radius < 3.6_GRID_SR) THEN
            r_S(1,i_globaldof) = (0.9_GRID_SR - 0.25_GRID_SR*r_radius) * GRID_GRAV
          ELSE
            r_S(1,i_globaldof) = 0.0_GRID_SR * GRID_GRAV
          END IF
          r_Q(1,i_globaldof) = MAX(0.0_GRID_SR, r_aux - r_S(1,i_globaldof))
        END DO
      END DO
    END IF

  END SUBROUTINE source_update

!*******************************************************************************
! DESCRIPTION of [SUBROUTINE dg_elmt_solution]:
!> @brief computes the exact solution approximated in the DG space to compare
!>        with in diagnostics
!>
!> @param[in]       r_coodof        coordinates where solution should be computed
!> @param[in]       r_time          model time
!> @param[in]       i_numelmt       number of elements
!> @param[in]       i_elmtdofs      DOFs of element
!> @param[in]       r_coonod        coordinates of element nodes
!> @param[in]       i_elmtnodes     nodes of element
!> @param           r_sol           computed exact solution
!
  SUBROUTINE dg_elmt_solution(r_coodof, r_time, i_numelmt, i_elmtdofs, r_coonod, i_elmtnodes, r_sol)

    IMPLICIT NONE

    REAL (KIND = GRID_SR),    DIMENSION(:,:), INTENT(IN)        :: r_coodof, r_coonod
    REAL (KIND = GRID_SR),                    INTENT(IN)        :: r_time
    INTEGER (KIND = GRID_SI),                 INTENT(IN)        :: i_numelmt
    INTEGER (KIND = GRID_SI), DIMENSION(:,:), INTENT(IN)        :: i_elmtdofs, i_elmtnodes
    REAL (KIND = GRID_SR),    DIMENSION(:,:), INTENT(INOUT)     :: r_sol

    r_sol = 0.0_GRID_SR

  END SUBROUTINE dg_elmt_solution

!*******************************************************************************
  FUNCTION boundaryhgt(r_time) RESULT(r_height)

    IMPLICIT NONE

    REAL(KIND = GRID_SR)                                  :: r_time, r_height

    REAL (KIND = GRID_SR)                                 :: r_h0, r_a, r_T, r_x0, r_c, r_K

    r_h0 = p_equationparam%r_depth

    r_a  = p_testparam%treal(1)%p_value(1)
    r_T  = p_testparam%treal(2)%p_value(1)
    r_x0 = p_testparam%treal(3)%p_value(1)

    r_c  = SQRT(GRID_GRAV*r_h0) * (1.0_GRID_SR + 0.5_GRID_SR*r_a/r_h0)
    r_K  = SQRT(0.75_GRID_SR*r_a/r_h0**3)

    r_height = r_a / COSH(r_K * (r_c*r_T - r_c*r_time - r_x0))**2

  END FUNCTION boundaryhgt

!*******************************************************************************
! Only internal subroutines follow
!*******************************************************************************
! DESCRIPTION of [SUBROUTINE field_init]:
!> @brief initializes the height and momentum fields
!>
!> @param[in,out]   p_param       control structure for global parameters
!> @param[in]       i_numelmt     total number of elements
!> @param           i_elmtdofs    global element-dof relation matrix
!> @param           r_coodof      coordinates of DOFs
!> @param[in,out]   r_Q           discrete solution vector
!> @param           r_S           discrete fields used in source terms
!
  SUBROUTINE field_init(p_param, i_numelmt, i_elmtdofs, r_coodof, r_Q, r_S)

    IMPLICIT NONE

    TYPE (control_struct),                    INTENT(inout) :: p_param
    INTEGER (KIND = GRID_SI),                 INTENT(in)    :: i_numelmt
    INTEGER (KIND = GRID_SI), DIMENSION(:,:), INTENT(in)    :: i_elmtdofs
    REAL (KIND = GRID_SR), DIMENSION(:,:),    INTENT(in)    :: r_coodof
    REAL (KIND = GRID_SR), DIMENSION(:,:),    INTENT(inout) :: r_Q
    REAL (KIND = GRID_SR), DIMENSION(:,:),    INTENT(in)    :: r_S

!--- local declarations
    INTEGER (KIND = GRID_SI)                                :: i_count, i_elmt, &
      i_faceunknowns

!--- initialize some constants
    i_faceunknowns = GRID_femtypes%p_type(FEM_DG)%sig%i_unknowns

    DO i_elmt=1, i_numelmt
      DO i_count=1, i_faceunknowns
        r_Q(1,i_elmtdofs(i_count,i_elmt)) = &
          MAX(0.0_GRID_SR, p_equationparam%r_depth*GRID_GRAV - r_S(1,i_elmtdofs(i_count,i_elmt)))
        r_Q(2,i_elmtdofs(i_count,i_elmt)) = r_Q(1,i_elmtdofs(i_count,i_elmt)) * 0.0_GRID_SR
        r_Q(3,i_elmtdofs(i_count,i_elmt)) = r_Q(1,i_elmtdofs(i_count,i_elmt)) * 0.0_GRID_SR
      END DO !i_elmt
    END DO !i_count

  END SUBROUTINE field_init

!*******************************************************************************
END MODULE DG_initial
